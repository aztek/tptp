%--------------------------------------------------------------------------
% File     : RNG003-0 : TPTP v7.2.0. Released v1.0.0.
% Domain   : Ring Theory (Alternative)
% Axioms   : Alternative ring theory (equality) axioms
% Version  : [Ste87] (equality) axioms.
% English  :

% Refs     : [Ste87] Stevens (1987), Some Experiments in Nonassociative Rin
% Source   : [Ste87]
% Names    :

% Status   : Satisfiable
% Syntax   : Number of clauses    :   15 (   0 non-Horn;  15 unit;   0 RR)
%            Number of atoms      :   15 (  15 equality)
%            Maximal clause size  :    1 (   1 average)
%            Number of predicates :    1 (   0 propositional; 2-2 arity)
%            Number of functors   :    6 (   1 constant; 0-3 arity)
%            Number of variables  :   27 (   2 singleton)
%            Maximal term depth   :    5 (   2 average)
% SPC      : 

% Comments :
%--------------------------------------------------------------------------
%----There exists an additive identity element
cnf(left_additive_identity,axiom,
    ( add(additive_identity,X) = X )).

cnf(right_additive_identity,axiom,
    ( add(X,additive_identity) = X )).

%----Multiplicative zero
cnf(left_multiplicative_zero,axiom,
    ( multiply(additive_identity,X) = additive_identity )).

cnf(right_multiplicative_zero,axiom,
    ( multiply(X,additive_identity) = additive_identity )).

%----Existence of left additive additive_inverse
cnf(left_additive_inverse,axiom,
    ( add(additive_inverse(X),X) = additive_identity )).

cnf(right_additive_inverse,axiom,
    ( add(X,additive_inverse(X)) = additive_identity )).

%----Inverse of additive_inverse of X is X
cnf(additive_inverse_additive_inverse,axiom,
    ( additive_inverse(additive_inverse(X)) = X )).

%----Distributive property of product over sum
cnf(distribute1,axiom,
    ( multiply(X,add(Y,Z)) = add(multiply(X,Y),multiply(X,Z)) )).

cnf(distribute2,axiom,
    ( multiply(add(X,Y),Z) = add(multiply(X,Z),multiply(Y,Z)) )).

%----Commutativity for addition
cnf(commutativity_for_addition,axiom,
    ( add(X,Y) = add(Y,X) )).

%----Associativity for addition
cnf(associativity_for_addition,axiom,
    ( add(X,add(Y,Z)) = add(add(X,Y),Z) )).

%----Right alternative law
cnf(right_alternative,axiom,
    ( multiply(multiply(X,Y),Y) = multiply(X,multiply(Y,Y)) )).

%----Left alternative law
cnf(left_alternative,axiom,
    ( multiply(multiply(X,X),Y) = multiply(X,multiply(X,Y)) )).

%----Associator
cnf(associator,axiom,
    ( associator(X,Y,Z) = add(multiply(multiply(X,Y),Z),additive_inverse(multiply(X,multiply(Y,Z)))) )).

%----Commutator
cnf(commutator,axiom,
    ( commutator(X,Y) = add(multiply(Y,X),additive_inverse(multiply(X,Y))) )).

%--------------------------------------------------------------------------
